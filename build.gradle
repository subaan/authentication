buildscript {
    ext {
            springBootVersion = '1.3.5.RELEASE'
            springRestDocsVersion = '1.0.1.RELEASE'
    }
    repositories {
            mavenCentral()
    }
    dependencies {
            classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
            classpath('io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE')
    }
}

//Add the AsciiDoctor plugin
plugins {
    id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'

//IDE
apply plugin: 'eclipse'
apply plugin: 'idea'


jar {
	baseName = 'demo'
	version = '0.0.1-SNAPSHOT'
    dependsOn asciidoctor  //make the jar task dependent on the AsciiDoctor task
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "http://repo.spring.io/libs-release-remote/" }
}

configurations {
    providedRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    compile "com.h2database:h2"
    compile "org.glassfish:javax.json:1.0.4"
    compile "javax.json:javax.json-api:1.0"

    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:${springRestDocsVersion}"
}

//Documentation snippets target folder
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
